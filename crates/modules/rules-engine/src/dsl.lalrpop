use validatron::{Operator, RelationalOperator, StringOperator, MultiOperator, Match, Field, Condition};
use lalrpop_util::ParseError;

use super::{DslError};

grammar(variant: &str);

pub Condition: Condition = {
    <l:Condition> "AND" <r:SignedCondition> => Condition::And{l: Box::new(l), r: Box::new(r)},
    <l:Condition> "OR" <r:SignedCondition> => Condition::Or{l: Box::new(l), r: Box::new(r)},
    SignedCondition
}

SignedCondition: Condition = {
    "NOT" <b:BaseCondition> => Condition::Not{inner: Box::new(b)},
    BaseCondition
}

BaseCondition: Condition = {
    <f: FieldPath> <op: Operator> <value: Value> => Condition::Base {
        field_path: f,
        op,
        value: Match::Value(value)
    },
    <f: FieldPath> <op: Operator> <value: FieldPath> => Condition::Base {
        field_path: f,
        op,
        value: Match::Field(value)
    },
    <f: FieldPath> "IN" <list: ValueList> =>? {
        let mut iterator = list.into_iter();

        let first = Condition::Base {
            field_path: f.clone(),
            op: Operator::Relational(RelationalOperator::Equals),
            value: iterator.next().map(|x| Match::Value(x)).ok_or(ParseError::User {
                error: DslError::EmptyList
            })?
        };
    
        let ret_val = iterator.fold(first, |acc, x| {
            Condition::Or {
                l: Box::new(acc),
                r: Box::new(Condition::Base {
                    field_path: f.clone(),
                    op: Operator::Relational(RelationalOperator::Equals),
                    value: Match::Value(x)
                })
            }
        });
    
        Ok(ret_val)
    },
    "(" <Condition> ")",
}

ValueList: Vec<String> = {
    "[" <Comma<Value>> "]" => <>
}

Comma<T>: Vec<T> = {
    <mut v:(<T> ",")*> <e:T?> => match e {
        None => v,
        Some(e) => {
            v.push(e);
            v
        }
    }
}

Value: String = {
    r#""\S+""# => <>.trim_matches('"').to_string(),
    r"[0-9]+" => <>.to_string()
}

Operator: Operator = {
    // Relational
    "==" => Operator::Relational(RelationalOperator::Equals),
    "!=" => Operator::Relational(RelationalOperator::NotEquals),
    ">" => Operator::Relational(RelationalOperator::Greater),
    "<" => Operator::Relational(RelationalOperator::Less),
    ">=" => Operator::Relational(RelationalOperator::GreaterEqual),
    "<=" => Operator::Relational(RelationalOperator::LessEqual),
    // String
    "STARTS_WITH" => Operator::String(StringOperator::StartsWith),
    "ENDS_WITH" => Operator::String(StringOperator::EndsWith),
    // Multi
    "CONTAINS" => Operator::Multi(MultiOperator::Contains),
}

FieldPath: Vec<Field> = {
    <Dot<Ident>> => {
        let mut payload_subpath = false;
        <>.into_iter().enumerate().map(|(index,value)| {
            if index == 0 && value == "payload" {
                payload_subpath = true;
            }
            if index == 1 && payload_subpath {
                Field::Adt{variant_name: variant.to_string(), field_name: value}
            } else {
               Field::Simple { field_name: value }
            }  
        }).collect()
    }
}

Dot<T>: Vec<T> = {
    <mut v:(<T> ".")*> <e:T?> => match e {
        None => v,
        Some(e) => {
            v.push(e);
            v
        }
    }
}

Ident: String = {
    <s: r"[a-zA-Z]+\w*"> => <>.to_string()
}

extern {
    type Error = DslError;
}